# position: 2
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX schema: <https://schema.org/>
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT 
?intellectual_entity_id 
?schema_copyright_holder
?created_at
?updated_at
WHERE {
  ?intellectual_entity_id a premis:IntellectualEntity;
    schema:copyrightHolder/(skos:prefLabel|schema:name) ?schema_copyright_holder_res.

  BIND (IF(regex(?schema_copyright_holder_res, "(\r\n|\r|\n)", "i"),CONCAT("\"",?schema_copyright_holder_res,"\""),?schema_copyright_holder_res) AS ?schema_copyright_holder)
  # Do not include images
  FILTER NOT EXISTS {
    ?intellectual_entity_id dct:format "image".
  }
  # Only include the right licenses
  FILTER EXISTS {
    ?intellectual_entity_id schema:license ?schema_license
    VALUES ?schema_license { 
        haLicId:VIAA-PUBLIEK-METADATA-LTD 
        haLicId:VIAA-PUBLIEK-METADATA-ALL
        haLicId:VIAA-PUBLIEK-CONTENT
        haLicId:BEZOEKERTOOL-METADATA-ALL
        haLicId:BEZOEKERTOOL-CONTENT 
        haLicId:VIAA-INTRA_CP-METADATA-ALL 
        haLicId:VIAA-INTRA_CP-CONTENT 
        haLicId:Publiek-Domein
        haLicId:COPYRIGHT-UNDETERMINED
    }
  }
  
  ?intellectual_entity_id prov:wasDerivedFrom/schema:dateModified ?modified.
  FILTER (!isLiteral(?since) || ?modified >= ?since )
}