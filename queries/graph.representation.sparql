# position: 3
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX schema: <https://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX haObj: <https://data.hetarchief.be/ns/object/> 
PREFIX haDes: <https://data.hetarchief.be/ns/descriptive/> 
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
PREFIX rel: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/> 
PREFIX ebucore: <http://www.ebu.ch/metadata/ontologies/ebucore/ebucore#> 
prefix prov: <http://www.w3.org/ns/prov#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
# EBUCore - the Dublin Core for media
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> # XML Schema Definition
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
prefix edm: <http://www.europeana.eu/schemas/edm/>

SELECT DISTINCT
?id
?edm_is_next_in_sequence 
?is_media_fragment_of
?premis_represents 
?relation_has_root 
?relation_has_source 
?schema_caption 
?schema_end_time
?schema_in_language 
?schema_name 
?schema_start_time
?schema_transcript
?created_at
?updated_at
WHERE {
    ?id a ?type;
          schema:name ?schema_name ;
          rel:rep ?premis_represents.

    VALUES ?type {haObj:DigitalRepresentation haObj:FragmentRepresentation}
          
    ?premis_represents schema:license ?schema_license_res . 

    FILTER langMatches(lang(?schema_name), "nl")
        
    VALUES ?schema_license_res { haLicId:VIAA-PUBLIEK-METADATA-LTD haLicId:VIAA-PUBLIEK-METADATA-ALL haLicId:MEEMOO-BEZOEKERTOOL-METADATA haLicId:MEEMOO-BEZOEKERTOOL-CONTENT haLicId:VIAA-INTRA_CP-METADATA-ALL haLicId:VIAA-INTRA_CP-CONTENT }

    ?id prov:wasDerivedFrom/schema:dateModified ?modified .
    FILTER (!isLiteral(?since) || ?modified >= ?since )

    OPTIONAL {
      ?id ebucore:isMediaFragmentOf ?is_media_fragment_of 
    }

    OPTIONAL {
      ?id schema:inLanguage ?schema_in_language
    }

    OPTIONAL {
      ?id rel:hsr ?relation_has_root
    }

    OPTIONAL {
      ?id rel:hss ?relation_has_source
    }

    OPTIONAL {
      ?id edm:isNextInSequence ?edm_is_next_in_sequence 
    }

    OPTIONAL {
      ?id schema:thumbnailUrl ?schema_thumbnail_url;
    }

    OPTIONAL {
      ?id schema:duration ?schema_duration 
    }

    OPTIONAL {
      ?id schema:caption ?schema_caption 
    }

    OPTIONAL {
      ?id schema:transcript ?schema_transcript 
    }
          
    OPTIONAL {
      ?id schema:startTime ?schema_start_time;
          schema:endTime ?schema_end_time 
    }
}