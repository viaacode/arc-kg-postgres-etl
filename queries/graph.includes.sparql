# position: 3
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX schema: <https://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX haObj: <https://data.hetarchief.be/ns/object/> 
PREFIX haDes: <https://data.hetarchief.be/ns/description/> 
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
PREFIX rel: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/> 
PREFIX ebucore: <http://www.ebu.ch/metadata/ontologies/ebucore/ebucore#> 
prefix prov: <http://www.w3.org/ns/prov#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
# EBUCore - the Dublin Core for media
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> # XML Schema Definition
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
prefix edm: <http://www.europeana.eu/schemas/edm/>

SELECT DISTINCT
?representation_id
?file_id
?created_at
?updated_at
FROM <https://data.hetarchief.be/graph/thesaurus/thesauri>
FROM <https://data.hetarchief.be/graph/thesaurus/licenses>
FROM <https://data.hetarchief.be/graph/organization>
FROM <https://data.hetarchief.be/graph/thesaurus/namenlijst>
FROM <https://data.hetarchief.be/graph/metadata/OR-0z70w1b>
FROM <https://data.hetarchief.be/graph/metadata/OR-zc7rt4f>
FROM <https://data.hetarchief.be/graph/metadata/OR-v97zq9j>
FROM <https://data.hetarchief.be/graph/metadata/OR-gh9b857>
WHERE {
    ?file_id a premis:File;
        ebucore:hasMimeType ?ebucore_has_mime_type ;
        rel:isi ?representation_id.
    
    ?representation_id haObj:isAccessCopyOf|^haObj:hasIIIFCopy|haObj:isTranscriptionCopyOf ?intellectual_entity_id .
    
    # Do not include images
    ?intellectual_entity_id dct:format ?format.
    VALUES ?format {
      #"image"
      "video"
      "videofragment"
      "audiofragment"
      "audio"
      "film"
      "newspaperpage"
      "newspaper"
    } 

    # Only include the right licenses
    FILTER EXISTS {
      ?intellectual_entity_id schema:license ?schema_license
      VALUES ?schema_license { 
        haLicId:VIAA-PUBLIEK-METADATA-LTD 
        haLicId:VIAA-PUBLIEK-METADATA-ALL
        haLicId:VIAA-PUBLIEK-CONTENT
        haLicId:BEZOEKERTOOL-METADATA-ALL
        haLicId:BEZOEKERTOOL-CONTENT 
        haLicId:VIAA-INTRA_CP-METADATA-ALL 
        haLicId:VIAA-INTRA_CP-CONTENT 
        haLicId:Publiek-Domein
        haLicId:COPYRIGHT-UNDETERMINED
      }
    }

    FILTER (
      (?ebucore_has_mime_type IN ( "application/json", "video/mp4", "audio/mp4", "image/jpeg", "application/xml", "audio/mpeg", "audio/m4a", "image/jph" ))
    )

    ?file_id prov:wasDerivedFrom/schema:dateModified ?modified .
    FILTER (!isLiteral(?since) || ?modified >= ?since )
}