# position: 2
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX schema: <https://schema.org/>
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT DISTINCT * {{
SELECT DISTINCT 
?intellectual_entity_id 
?schema_alternate_name
?created_at
?updated_at
FROM <https://data.hetarchief.be/graph/thesaurus/thesauri>
FROM <https://data.hetarchief.be/graph/thesaurus/licenses>
FROM <https://data.hetarchief.be/graph/organization>
FROM <https://data.hetarchief.be/graph/thesaurus/namenlijst>
FROM <https://data.hetarchief.be/graph/metadata/OR-0z70w1b>
FROM <https://data.hetarchief.be/graph/metadata/OR-zc7rt4f>
FROM <https://data.hetarchief.be/graph/metadata/OR-v97zq9j>
FROM <https://data.hetarchief.be/graph/metadata/OR-gh9b857>
WHERE {
  ?intellectual_entity_id a premis:IntellectualEntity;
    schema:alternateName ?schema_alternate_name_res.

  FILTER langMatches(lang(?schema_alternate_name_res), "nl")

  BIND (IF(regex(?schema_alternate_name_res, "(\r\n|\r|\n)", "i"),CONCAT("\"",?schema_alternate_name_res,"\""),?schema_alternate_name_res) AS ?schema_alternate_name)
  
  #Do not include images
  ?intellectual_entity_id dct:format ?format.
  VALUES ?format {
    #"image"
    "video"
    "videofragment"
    "audiofragment"
    "audio"
    "film"
    "newspaperpage"
    "newspaper"
  } 
  # Only include the right licenses
  FILTER EXISTS {
    ?intellectual_entity_id schema:license ?schema_license
    VALUES ?schema_license { 
        haLicId:VIAA-PUBLIEK-METADATA-LTD 
        haLicId:VIAA-PUBLIEK-METADATA-ALL
        haLicId:VIAA-PUBLIEK-CONTENT
        haLicId:BEZOEKERTOOL-METADATA-ALL
        haLicId:BEZOEKERTOOL-CONTENT 
        haLicId:VIAA-INTRA_CP-METADATA-ALL 
        haLicId:VIAA-INTRA_CP-CONTENT 
        haLicId:Publiek-Domein
        haLicId:COPYRIGHT-UNDETERMINED
    }
  }
  
  ?intellectual_entity_id prov:wasDerivedFrom/schema:dateModified ?modified.
  FILTER (!isLiteral(?since) || ?modified >= ?since )
}
}}