# position: 2
PREFIX schema: <https://schema.org/>
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ebucore: <http://www.ebu.ch/metadata/ontologies/ebucore/ebucore#>

SELECT DISTINCT 
?intellectual_entity_id 
?thing_id
?confidence
?height
?width
?x
?y
?created_at
?updated_at
WHERE {
    ?intellectual_entity_id schema:mentions ?thing_id.

    ?annotation ebucore:isAnnotatedMediaResource ?resource;
                ebucore:width ?width;
                ebucore:height ?height;
                ebucore:regionDelimX ?x;
                ebucore:regionDelimY ?y.
                
    
    ?resource ebucore:annotationConfidence ?confidence;
              ebucore:hasAnnotationAgent ?thing_id.                
  
    # Do not include images
    ?intellectual_entity_id dct:format ?format.
    VALUES ?format {
      #"image"
      "video"
      "videofragment"
      "audiofragment"
      "audio"
      "film"
      "newspaperpage"
      "newspaper"
    } 
    # Only include the right licenses
    FILTER EXISTS {
      ?intellectual_entity_id schema:license ?schema_license
      VALUES ?schema_license { 
        haLicId:VIAA-PUBLIEK-METADATA-LTD 
        haLicId:VIAA-PUBLIEK-METADATA-ALL
        haLicId:VIAA-PUBLIEK-CONTENT
        haLicId:BEZOEKERTOOL-METADATA-ALL
        haLicId:BEZOEKERTOOL-CONTENT 
        haLicId:VIAA-INTRA_CP-METADATA-ALL 
        haLicId:VIAA-INTRA_CP-CONTENT 
        haLicId:Publiek-Domein
        haLicId:COPYRIGHT-UNDETERMINED
    }
    }

    ?intellectual_entity_id prov:wasDerivedFrom/schema:dateModified ?modified .
    FILTER (!isLiteral(?since) || ?modified >= ?since )
}