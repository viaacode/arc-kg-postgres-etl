# position: 2
prefix schema: <https://schema.org/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>

SELECT DISTINCT 
  ?id
  ?bibframe_preceeded_by 
  ?bibframe_succeeded_by 
  ?bibframe_supplement_to
  ?collection_type
  ?schema_alternate_name 
  (MAX(?schema_end_date_res) AS ?schema_end_date)
  ?schema_identifier 
  ?schema_is_part_of
  (SAMPLE(?schema_location_created_res) AS ?schema_location_created)
  (SAMPLE(?schema_name_res) AS ?schema_name) # TEMP FIX for multiple newspaper names
  ?schema_publisher 
  ?schema_season_number 
  (MIN(?schema_start_date_res) AS ?schema_start_date)
  ?created_at
  ?updated_at
WHERE {
  {
    ?id a schema:Newspaper;
        schema:identifier ?schema_identifier;
        schema:name ?schema_name_res;
        schema:hasPart ?serie.

    ?serie 
        schema:locationCreated/schema:name ?schema_location_created_res;
        schema:startDate ?schema_start_date_res;
        schema:endDate ?schema_end_date_res;
        schema:publisher ?schema_publisher.

    FILTER langMatches(lang(?schema_location_created_res), "nl")

    #OPTIONAL { ?id bf:preceededBy ?bibframe_preceeded_by }

    #OPTIONAL { ?id bf:succeededBy ?bibframe_succeeded_by }

    #OPTIONAL { ?id bf:supplementTo ?bibframe_supplement_to } 
    BIND ("newspaper" AS ?collection_type)
  }
  UNION {
    ?id a schema:Episode; schema:name ?schema_name_res.
    BIND ("episode" AS ?collection_type)
  }
  UNION {
    ?id a schema:CreativeWorkSeries; schema:name ?schema_name_res.
    OPTIONAL { ?id schema:isPartOf ?schema_is_part_of }
    BIND ("series" AS ?collection_type)
  }
  UNION {
    ?id a schema:CreativeWorkSeason; schema:name ?schema_name_res;
    OPTIONAL { ?id schema:seasonNumber ?schema_season_number }
    BIND ("season" AS ?collection_type)
  }
  UNION {
    ?id a schema:ArchiveComponent; schema:name ?schema_name_res.
    OPTIONAL { ?id schema:isPartOf ?schema_is_part_of }
    BIND ("archive" AS ?collection_type)
  }
  #?x schema:isPartOf ?id.
}
GROUP BY ?id
  ?bibframe_preceeded_by 
  ?bibframe_succeeded_by 
  ?bibframe_supplement_to
  ?collection_type
  ?schema_alternate_name 
  ?schema_identifier 
  ?schema_is_part_of
  #?schema_location_created
  ?schema_publisher 
  ?schema_season_number 
  ?created_at
  ?updated_at