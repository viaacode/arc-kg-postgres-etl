# position: 2
prefix schema: <https://schema.org/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>

SELECT DISTINCT 
  ?id
  ?bibframe_preceeded_by 
  ?bibframe_succeeded_by 
  ?bibframe_supplement_to
  ?collection_type
  ?schema_alternate_name 
  (MAX(?schema_end_date_res) AS ?schema_end_date)
  ?schema_identifier 
  ?schema_is_part_of
  (SAMPLE(?schema_location_created_res) AS ?schema_location_created)
  (SAMPLE(?schema_name_res) AS ?schema_name) # TEMP FIX for multiple newspaper names
  (SAMPLE(?schema_publisher_res) AS ?schema_publisher)
  ?schema_season_number 
  (MIN(?schema_start_date_res) AS ?schema_start_date)
  ?created_at
  ?updated_at
FROM <https://data.hetarchief.be/graph/thesaurus/thesauri>
FROM <https://data.hetarchief.be/graph/thesaurus/licenses>
FROM <https://data.hetarchief.be/graph/organization>
FROM <https://data.hetarchief.be/graph/thesaurus/namenlijst>
FROM <https://data.hetarchief.be/graph/metadata/OR-0z70w1b>
FROM <https://data.hetarchief.be/graph/metadata/OR-zc7rt4f>
FROM <https://data.hetarchief.be/graph/metadata/OR-v97zq9j>
FROM <https://data.hetarchief.be/graph/metadata/OR-gh9b857>
WHERE {
  {
    ?id a schema:Newspaper;
        schema:identifier ?schema_identifier;
        schema:name ?schema_name_res;
        schema:hasPart ?serie.

    OPTIONAL {
      ?serie schema:locationCreated/schema:name ?schema_location_created_res.
      FILTER langMatches(lang(?schema_location_created_res), "nl")
    }

    OPTIONAL {    
      ?serie  schema:endDate ?schema_end_date_res.
    }

    OPTIONAL {
      ?serie schema:startDate ?schema_start_date_res.
    }

    OPTIONAL {
      ?serie schema:publisher ?schema_publisher_res.
    }

    #OPTIONAL { ?id bf:preceededBy ?bibframe_preceeded_by }

    #OPTIONAL { ?id bf:succeededBy ?bibframe_succeeded_by }

    #OPTIONAL { ?id bf:supplementTo ?bibframe_supplement_to } 
    BIND ("newspaper" AS ?collection_type)
  }
  UNION {
    ?id a schema:Episode; schema:name ?schema_name_res.
    BIND ("episode" AS ?collection_type)
  }
  UNION {
    ?id a schema:CreativeWorkSeries; schema:name ?schema_name_res.
    OPTIONAL { ?id schema:isPartOf ?schema_is_part_of }
    BIND ("series" AS ?collection_type)
  }
  UNION {
    ?id a schema:CreativeWorkSeason; schema:name ?schema_name_res;
    OPTIONAL { ?id schema:seasonNumber ?schema_season_number }
    BIND ("season" AS ?collection_type)
  }
  UNION {
    ?id a schema:ArchiveComponent; schema:name ?schema_name_res.
    OPTIONAL { ?id schema:isPartOf ?schema_is_part_of }
    BIND ("archive" AS ?collection_type)
  }
  #?x schema:isPartOf ?id.
}
GROUP BY ?id
  ?bibframe_preceeded_by 
  ?bibframe_succeeded_by 
  ?bibframe_supplement_to
  ?collection_type
  ?schema_alternate_name 
  ?schema_identifier 
  ?schema_is_part_of
  ?schema_season_number 
  ?created_at
  ?updated_at