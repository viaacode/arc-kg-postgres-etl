PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX schema: <https://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX haObj: <https://data.hetarchief.be/ns/object/> 
PREFIX haDes: <https://data.hetarchief.be/ns/descriptive/> 
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
PREFIX rel: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/> 
PREFIX ebucore: <http://www.ebu.ch/metadata/ontologies/ebucore/ebucore#> 
prefix prov: <http://www.w3.org/ns/prov#>
# EBUCore - the Dublin Core for media
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> # XML Schema Definition
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT
?id
?premis_identifier
?premis_medium
?schema_height
?schema_identifier
?schema_material
?schema_material_extent
?schema_name
?schema_width
WHERE {
    ?id a haObj:PhysicalCarrier;
    schema:name ?schema_name;
    premis:medium/skos:prefLabel ?premis_medium.

    FILTER (langMatches(lang(?schema_name), "nl") && langMatches(lang(?premis_medium), "nl"))

    OPTIONAL {
      ?id schema:identifier    ?schema_identifier
    }

    OPTIONAL {
      ?id premis:identifier ?premis_identifier
    }
  
    OPTIONAL {
      ?id schema:height ?height.
      ?height schema:value ?height_value;
              schema:unitText ?height_unit.
      BIND( CONCAT(?height_value, ' ', ?height_unit) as ?schema_height)
    }
    
  
    OPTIONAL {
      ?id schema:width ?width.
      ?width schema:value ?width_value;
              schema:unitText ?width_unit.
      BIND( CONCAT(?width_value, ' ', ?width_unit) as ?schema_width)
    }
    
    OPTIONAL {
      ?id schema:materialExtent ?schema_material_extent
    }
    OPTIONAL {
      ?id schema:material ?schema_material
    }
}