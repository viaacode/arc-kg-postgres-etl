PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX schema: <https://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX haObj: <https://data.hetarchief.be/ns/object/> 
PREFIX haDes: <https://data.hetarchief.be/ns/description/> 
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
PREFIX haEvt: <https://data.hetarchief.be/id/event-type/>
PREFIX rel: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/> 
PREFIX ebucore: <http://www.ebu.ch/metadata/ontologies/ebucore/ebucore#> 
prefix prov: <http://www.w3.org/ns/prov#>
# EBUCore - the Dublin Core for media
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> # XML Schema Definition
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX eror: <http://id.loc.gov/vocabulary/preservation/eventRelatedObjectRole/>

SELECT DISTINCT
?id
?bibframe_production_method
?digitization_date
?ebucore_has_captioning
?intellectual_entity_id
?premis_medium
?schema_height
?schema_identifier
?schema_material
?schema_material_extent
?schema_name
?schema_width
?type
?created_at
?updated_at
WHERE {
    ?intellectual_entity_id rel:isr ?rep;  
      schema:license ?schema_license;
      dct:format ?format.

    # Do not include images
    VALUES ?format {
      #"image"
      "video"
      "videofragment"
      "audiofragment"
      "audio"
      "film"
      "newspaperpage"
      "newspaper"
    } 

    # Only include the right licenses
    VALUES ?schema_license { 
      haLicId:VIAA-PUBLIEK-METADATA-LTD 
      haLicId:VIAA-PUBLIEK-METADATA-ALL
      haLicId:VIAA-PUBLIEK-CONTENT
      haLicId:BEZOEKERTOOL-METADATA-ALL
      haLicId:BEZOEKERTOOL-CONTENT 
      haLicId:VIAA-INTRA_CP-METADATA-ALL 
      haLicId:VIAA-INTRA_CP-CONTENT 
      haLicId:Publiek-Domein
      haLicId:COPYRIGHT-UNDETERMINED
    }
    
    ?rep premis:storedAt ?id.
    
    ?id a haObj:PhysicalCarrier;
    schema:name ?schema_name;
    premis:medium/skos:prefLabel ?premis_medium.

    FILTER (langMatches(lang(?schema_name), "nl") && langMatches(lang(?premis_medium), "en"))

    OPTIONAL {
      ?id schema:identifier ?schema_identifier
    }

    OPTIONAL {
      ?event a haEvt:digitization; 
        prov:startedAtTime ?digitization_date; 
        eror:sou ?id .
    }

    OPTIONAL {
      ?rep bf:productionMethod/skos:prefLabel ?bibframe_production_method.
      FILTER (langMatches(lang(?bibframe_production_method), "nl"))
    }
  
    OPTIONAL {
      ?id schema:height ?height.
      ?height schema:value ?height_value;
              schema:unitText ?height_unit.
      BIND( CONCAT(?height_value, ' ', ?height_unit) as ?schema_height)
    }
    
  
    OPTIONAL {
      ?id schema:width ?width.
      ?width schema:value ?width_value;
              schema:unitText ?width_unit.
      BIND( CONCAT(?width_value, ' ', ?width_unit) as ?schema_width)
    }
    
    OPTIONAL {
      ?id schema:materialExtent ?schema_material_extent
    }
    OPTIONAL {
      ?id schema:material ?schema_material
    }

    BIND(EXISTS {
      ?id ebucore:hasCaptioning/rdf:type ebucore:OpenCaptions 
    } AS ?ebucore_has_captioning)

    OPTIONAL {
      ?id a ?type_res.
      VALUES ?type_res { haDes:ImageReel haDes:AudioReel}
      ?type_res rdfs:label ?type.
      FILTER (langMatches(lang(?type), "nl"))
    }

}