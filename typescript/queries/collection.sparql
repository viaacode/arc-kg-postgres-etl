# position: 2
prefix schema: <https://schema.org/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
prefix prov: <http://www.w3.org/ns/prov#>
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>

PREFIX : <urn:kg-to-postgres:>

CONSTRUCT {
  ?id :tableName "graph.collection";
  :id ?id;
  :bibframe_preceeded_by ?bibframe_preceeded_by;
  :bibframe_succeeded_by ?bibframe_succeeded_by; 
  :bibframe_supplement_to ?bibframe_supplement_to;
  :collection_type ?collection_type;
  :schema_end_date ?schema_end_date;
  :schema_identifier ?schema_identifier ;
  :schema_is_part_of ?schema_is_part_of;
  :schema_location_created ?schema_location_created;
  :schema_name ?schema_name;
  :schema_publisher ?schema_publisher;
  :schema_season_number ?schema_season_number;
  :schema_start_date ?schema_start_date.

  ?il :tableName "graph.newspaper_schema_in_language";
  :collection_id ?id;
  :schema_in_language ?schema_in_language.

  ?an :tableName "graph.newspaper_schema_alternate_name";
  :collection_id ?id;
  :schema_alternate_name ?schema_alternate_name.

  ?ie_isp :tableName "graph.schema_is_part_of";
  :intellectual_entity_id ?ie;
  :type ?collection_type;
  :collection_id ?id.
}
WHERE {
    # Only get collections where the IEs are disseminated
   ?ie schema:license ?schema_license;
       dct:format ?format ;
       prov:wasDerivedFrom/schema:dateModified ?modified;
       schema:isPartOf ?id.

    # Only include the right licenses
    FILTER (?schema_license IN ( 
        haLicId:VIAA-PUBLIEK-METADATA-LTD, 
        haLicId:VIAA-PUBLIEK-METADATA-ALL,
        haLicId:VIAA-PUBLIEK-CONTENT,
        haLicId:BEZOEKERTOOL-METADATA-ALL,
        haLicId:BEZOEKERTOOL-CONTENT ,
        haLicId:VIAA-INTRA_CP-METADATA-ALL ,
        haLicId:VIAA-INTRA_CP-CONTENT ,
        haLicId:Publiek-Domein,
        haLicId:COPYRIGHT-UNDETERMINED
    ) && ?format != "image")

    # for incremental 
    FILTER (!BOUND(?since) || ?modified >= ?since )

    VALUES ?collection_type_res { 
      schema:Newspaper 
      schema:Episode 
      schema:ArchiveComponent 
      schema:CreativeWorkSeries
      schema:CreativeWorkSeason
    }
    ?id a ?collection_type_res; schema:name ?schema_name.

    BIND(lcase(REPLACE(STR(?collection_type_res), "^.*/([^/]*)$", "$1")) AS ?collection_type)
    BIND(IRI(CONCAT(str(?ie),"/",md5(str(?id)))) AS ?ie_isp)

    OPTIONAL {
      ?id a schema:Newspaper;
          schema:identifier ?schema_identifier;
          schema:hasPart ?serie.

      OPTIONAL {
        ?id schema:inLanguage ?schema_in_language.
        BIND(IRI(CONCAT(str(?id),md5(?schema_in_language))) AS ?il)
      }
  
      OPTIONAL {
        ?id schema:alternateName ?schema_alternate_name.
        BIND(IRI(CONCAT(str(?id),md5(?schema_alternate_name))) AS ?an)
      }
  
      OPTIONAL {
        ?serie schema:locationCreated/schema:name ?schema_location_created_res.
        FILTER langMatches(lang(?schema_location_created_res), "nl")
      }
  
      OPTIONAL { ?serie schema:endDate ?schema_end_date. }
      OPTIONAL { ?serie schema:startDate ?schema_start_date. }
      OPTIONAL { ?serie schema:publisher ?schema_publisher. }
  
      #OPTIONAL { ?id bf:preceededBy ?bibframe_preceeded_by }
  
      #OPTIONAL { ?id bf:succeededBy ?bibframe_succeeded_by }
  
      #OPTIONAL { ?id bf:supplementTo ?bibframe_supplement_to } 
    }
        
    OPTIONAL { ?id schema:seasonNumber ?schema_season_number }
    OPTIONAL { ?id schema:isPartOf ?schema_is_part_of }
  
}